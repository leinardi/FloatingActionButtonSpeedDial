/*
 * Copyright 2018 Roberto Leinardi.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply from: rootProject.file('versions-plugin.gradle')
apply plugin: "se.bjurr.violations.violation-comments-to-github-gradle-plugin"

buildscript {
    repositories {
        apply from: 'dependencies.gradle'
        addRepos(repositories)
    }
    dependencies {
        classpath deps.android_gradle_plugin
        classpath deps.android_checkstyle_plugin
        classpath deps.android_maven_gradle_plugin
        classpath deps.gradle_bintray_plugin
        classpath deps.gradle_versions_plugin
        classpath deps.violations_plugin
        classpath deps.errorprone_plugin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    repositories {
        addRepos(repositories)
    }
}

subprojects {
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex')
                    && !gradle.startParameter.taskNames.contains('dependencyUpdates')) {
                details.useVersion versions.support
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task violationCommentsToGitHub(type: se.bjurr.violations.comments.github.plugin.gradle.ViolationCommentsToGitHubTask) {
    repositoryOwner = "leinardi"
    repositoryName = "FloatingActionButtonSpeedDial"
    pullRequestId = System.properties['GITHUB_PULLREQUESTID']
    oAuth2Token = System.properties['GITHUB_OAUTH2TOKEN']
    gitHubUrl = "https://api.github.com/"
    createCommentWithAllSingleFileComments = false
    createSingleFileComments = true
    commentOnlyChangedContent = true
    violations = [
            ["FINDBUGS", ".", ".*/reports/findbugs/.*\\.xml\$", "Findbugs"],
            ["CHECKSTYLE", ".", ".*/reports/checkstyle/.*debug\\.xml\$", "Checkstyle"],
            ["ANDROIDLINT", ".", ".*/reports/lint-results.*\\.xml\$", "Android Lint"],
            ["GOOGLEERRORPRONE", ".", ".*/build.log\$", "Error Prone"]
    ]
}
